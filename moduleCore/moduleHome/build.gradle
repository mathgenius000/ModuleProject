//apply plugin: 'com.android.library'
//根据isModule标签动态的切换 集成/组件模式
if (!rootProject.isModule) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}
apply plugin: 'com.jakewharton.butterknife'

def config = rootProject.ext.android//配置
def librarys = rootProject.ext.dependencies//依赖

android {
    compileSdkVersion config.compileSdkVersion
    buildToolsVersion config.buildToolsVersion

    defaultConfig {

        //组件模式下 有应用Id
        if (!rootProject.isModule) {
            applicationId rootProject.appId.home
        }
        //资源文件冲突解决
//        if (rootProject.isModule) {
//            resourcePrefix 'module_home_'
//        }

        minSdkVersion config.minSdkVersion
        targetSdkVersion config.targetSdkVersion
        versionCode config.versionCode
        versionName config.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        buildConfigField "boolean", "IS_MODULE", rootProject.isModule.toString()

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }
    }

    //资源配置
    sourceSets {
        main {
            //在组件模式下 使用不同的manifest文件
            if (rootProject.isModule) {
                manifest.srcFile 'src/main/module/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation librarys.appcompat
    testImplementation librarys.junit
    androidTestImplementation librarys.junitTest
    androidTestImplementation librarys.espressoTest
    implementation librarys.supportv4
    implementation librarys.supportv7
    implementation librarys.design
    implementation librarys.constraintlayout

    implementation project(path: ':moduleSystem:moduleBase')
    implementation project(path: ':modulePublic:moduleCommon')

    annotationProcessor librarys.butterknifeCompiler
    annotationProcessor librarys.arouterCompiler
}